# Maintainer: Mattia Procopio (astro.matto) <matto.astro at gmail dot com>
pkgname=kstars
pkgver=3.5.7
pkgrel=2
pkgdesc="Kstars planetary software and ekos"
arch=(x86_64 aarch64)
url="https://edu.kde.org/kstars/"
license=('APACHE2' 'BSD3' 'GPL' 'LGPL' 'LGPLv2' 'LGPLv3' 'MIT')
depends=('breeze-icons' 'hicolor-icon-theme' 'knewstuff' 'knotifyconfig' 'kplotting' \
         'libindi' 'libraw' 'qt5-datavis3d' 'qt5-quickcontrols' 'qt5-websockets' 'qtkeychain-qt5' \
	 'stellarsolver' 'xplanet' 'eigen' 'kdoctools')
makedepends=(cmake extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('aa8f122ee9b8e81334433191eab276c361112b9dcb9bcf8dfa0851e87a100d06')

build() {
  cmake -B build -S $pkgname-$pkgver \
	-DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
        -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
